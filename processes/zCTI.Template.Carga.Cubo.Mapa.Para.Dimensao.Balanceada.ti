#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#GENERALCOMMENT             Descrição:          	Atualiza dimensão ACO.D.Conta_Contabil
#GENERALCOMMENT             Responsável:   	Rodrigo Mendonça
#GENERALCOMMENT             Data Criação:    	Fevereiro/2020
#
#LASTCHANGE                 Alterações:
#LASTCHANGE                 Responsavel Alterações:
# ===============================================================================================================

# ===============================================================================================================
#PROLOGCOMMENT               Descrição:            		Atualiza dimensão de hierarquia balanceada ACO.D.Centro_Custo através 
#PROLOGCOMMENT               	            		do mapa ACO.D.Centro_Custo.
# ===============================================================================================================

# =================================================================================
# 									Glosário 
# =================================================================================
# Prefixos de Variáveis 
#
# v - Variáveis de entrada		- relacionado a origem de dados do turbo
# p - Parâmetros de entrada		- relacionado ao input realizado pelo usuário no momento de disparo do processo
# c - Constantes			- variáveis que receberão um valor fixo (Não serão alteradas durante o processo)
# n - Numerica
# s - String (Texto)
#
# Variáveis que deverão ser alteradas
# cDimensao			- Armazena o nome da dimensão
# cCuboMapa			- Armazena o nome do cubo de origem
# cTotalizador			- Armazena o nome do totalizador da dimensão
#
#
# Variáveis que não precisam ser alteradas
# cCuboParametro 			- Armazena o nome do cubo de parâmetro utilizado
# cHorarioInicio			- Armazena o horário de inicio do processo
# cMensagemRegistro		- Armazena a mensagem de erro/log do registro
# cMensagemProcesso		- Armazena a mensagem de erro do processo
# cProcesso			- Armazena o nome do processo
# cUsuario			- Armazena o nome do usuário ou chore que executou o processo

# =================================================================================
# Variáveis do padrão CTI que devem ser alteradas
# =================================================================================


# Informações da dimensão e cubo mapa
# ========================================
cDimensao		= 'ACO.D.Centro_Custo';
cCuboMapa		= 'MAP.010.Centro_Custo';

cTotalizador		= 'Total Centros de Custo';


# =================================================================================
# Variáveis do padrão CTI não devem ser alteradas
# =================================================================================

# Recupera o usuário que executou o processo
# ========================================
cUsuario = IF( DimIx( '}Clients' , TM1User() ) = 0, 'Executed by Chore' , ATTRS('}Clients', TM1User(), '}TM1_DefaultDisplayValue') );
  

  
# Variáveis de controle do processo
# ========================================
cCuboParametro			= 'SYS.150.Controle_Cargas';
cHorarioInicio			= Now();
cProcesso			= GetProcessName();
cMensagemProcesso 		= '';
cContadorRegistro 			= 0;



# =================================================================================
# Define o Log inicial da carga como falha
# Registra esse log logo no inicio para garantir que tenha algum log de falha registrado
# Caso o processo não consiga chegar no próximo registro de log por alguma falha inesperada
# =================================================================================
cMensagemProcesso	= 'Processo com falha, contate o administrador para verificar os logs de erro do sistema' ;
s01			= TimSt( cHorarioInicio , '\Y-\m-\d \hh\im\ss' );
s02 			= cUsuario ;
s03 			= cMensagemProcesso ;
s04 			= '0' ;
s05 			= TimSt( cHorarioInicio - Now, '\Y-\m-\d \hh\im\ss' );

CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Inicio' );
CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Fim' );
CellPutS( s02 , cCuboParametro, cProcesso, 'Usuário' );
CellPutS( s03 , cCuboParametro, cProcesso, 'Mensagem de Log' );
CellPutS( s04 , cCuboParametro, cProcesso, 'Registros Processados' );
CellPutS( s05 , cCuboParametro, cProcesso, 'Tempo de Execução' );



# =================================================================================
# Limpa todos consolidadores da dimensão
# =================================================================================
#** Busca a Qtde de Elementos na dimensão
      vElemento = DimSiz(cDimensao);
      vCount = vElemento;
#** Checa todos os elementos da dimensão e recria somente elementos dos níveis maiores
      WHILE(vCount <> 0);
      vElementoN = DimNm(cDimensao, vCount);
                             IF(DTYPE(cDimensao, vElementoN) @= 'C');
                                 DimensionElementDelete(cDimensao, vElementoN);
                             ENDIF;
      vCount = vCount - 1;
      END;



# =================================================================================
# Insere Totalizador na Dimensão 
# =================================================================================
DimensionElementInsert ( cDimensao, '', cTotalizador, 'C' );




# =================================================================================
# Define a Ordem da Dimensão 
# =================================================================================
DimensionSortOrder ( cDimensao, 'ByName', 'Ascending', 'ByHierarchy' , 'Ascending' );


#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#METADATACOMMENT        Descrição:  Atualiza estrutura da dimensão desbalanceada considerando os niveis existentes no cubo mapa.
# ===============================================================================================================



# =================================================================================
# Atualiza dimensão 
# =================================================================================

sElm = vElement;
sDim = cDimensao;
sTot = cTotalizador;


# Adiciona elemento
# ==========================
DimensionElementComponentAdd ( sDim, sTot, sElm, 1 );
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#DATACOMMENT        Descrição:  Atualiza alias e atributos da dimensão.
# ===============================================================================================================



# ===================================================================================
# Gera contador para quantidade total de registros processados
# ===================================================================================
cContadorRegistro = cContadorRegistro + 1;
sContadorRegistro = NumberToString( cContadorRegistro );


# =================================================================================
# Busca Variáveis do Cubo Mapa
# =================================================================================

# Alias e Atributos
#==========================================
sDescElement	= CellGetS ( cCuboMapa, vElement, 'Descrição' );



# =================================================================================
# Adiciona Alias com codigo e descrição
# =================================================================================

# Adiciona Alias Cod+Desc
#==========================================
sDim	= cDimensao;
sAlias	= 'Código e Descrição';


#Element
#===========
sCod   = vElement;
sDesc = sDescElement;

If  (  sCod @<>'' & sDesc @<> '' );
    AttrPutS( sCod | ' - ' | sDesc, sDim, sCod, sAlias );
EndIf;




#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#EPILOGCOMMENT        Descrição:  Habilita log do cubo de destino, realiza CubeSaveData e registra log de status do processo.
# ===============================================================================================================



# =================================================================================
# Registra o Log de Erro ou Sucesso no Cubo de Controle
# =================================================================================
cMensagemProcesso  = ' Processo concluído com sucesso';


s01	= TimSt( cHorarioInicio , '\Y-\m-\d \hh\im\ss' );
s02 	= cUsuario ;
s03 	= cMensagemProcesso ;
s04 	= sContadorRegistro  ;
s05 	= TimSt( Now - cHorarioInicio, ' \h:\i:\s') ;

CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Fim' );
CellPutS( s02 , cCuboParametro, cProcesso, 'Usuário' );
CellPutS( s03 , cCuboParametro, cProcesso, 'Mensagem de Log' );
CellPutS( s04 , cCuboParametro, cProcesso, 'Registros Processados' );
CellPutS( s05 , cCuboParametro, cProcesso, 'Tempo de Execução' );



# =================================================================================
# Executa processo que consolida elementos orfaos
# =================================================================================
ExecuteProcess
  ( 'zCTI.Dim.Hierarchy.Consolidate.Orphans'
  , 'pDim'			, cDimensao
  , 'pTotalizador'		, 'Total Histórico'
  , 'pTotalizadorFilho'	, ''
   );
#endregion