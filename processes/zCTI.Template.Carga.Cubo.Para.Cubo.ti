#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#GENERALCOMMENT             Descrição:          	Carrega dados do cubo ACO.200.Despesas para o cubo ACO.300.Despesas
#GENERALCOMMENT             Responsável:   	Hugo Venceslau
#GENERALCOMMENT             Data Criação:    	Junho/2019
#
#LASTCHANGE                 Alterações:
#LASTCHANGE                 Responsavel Alterações:
# ===============================================================================================================

# ===============================================================================================================
#PROLOGCOMMENT               Descrição:            	Cria view de leitura utlizando como filtro Parametros de Versão, Ano e Mês.
# ===============================================================================================================

# =================================================================================
# 									Glosário 
# =================================================================================
# Prefixos de Variáveis 
#
# v - Variáveis de entrada		- relacionado a origem de dados do turbo
# p - Parâmetros de entrada		- relacionado ao input realizado pelo usuário no momento de disparo do processo
# c - Constantes			- variáveis que receberão um valor fixo (Não serão alteradas durante o processo)
# n - Numerica
# s - String (Texto)
#
# Variáveis que deverão ser alteradas
# cCuboOrigem			- Armazena o nome do cubo de origem
# cCuboDestino			- Armazena o nome do cubo de destino
# cSuprimirConsolidado		- (S/N) - Suprimir a informação dos elementos consolidados
# cSuprimirZero			- (S/N) - Suprimir os valores 0 da view
# cSuprimirCamposCalculados		- (S/N) - Suprimir os campos calculados 
# cViewTemporaria			- (S/N) - Se a view deve ser temporária 
#
#
# Variáveis configuradas no cubo de controle 
# cCaminhoArquivoRejeitado 		- Busca o caminho de rejeitado do processo no cubo de controle
# cUtilizarDebug		 	- Busca no cubo de controle se ao executar o processo, será utilizado o modo Debug (Grava em arquivo texto o que seria gravado no cubo)
# cGravarLogAlteracao		- Busca no cubo de controle se ao executar o processo, serão gravados as alterações no cubo de destino
#
#
# Variáveis que não precisam ser alteradas
# cCuboParametro 			- Armazena o nome do cubo de parâmetro utilizado
# cCuboPropriedade		- Armazena o nome do cubo de sistema onde estão as principais propriedades dos cubos
# cExisteCabecalhoRejeitado		- Variável que irá controlar o cabeçalho do arquivo de rejeitado sejá criado uma única vez
# cExisteRegistroRejeitado 		- Variável que irá controlar se o registro possuí algum erro de integridade ou validação dos dados
# cHorarioInicio			- Armazena o horário de inicio do processo
# cMensagemRegistro		- Armazena a mensagem de erro/log do registro
# cMensagemProcesso		- Armazena a mensagem de erro do processo
# cProcesso			- Armazena o nome do processo
# cQtdRegistroRejeitado		- Contador de registros rejeitados
# cTipoErro			- Controla o tipo de erro (Erro 1 - Prologo / Erro 2 - Dados)
# cUsuario			- Armazena o nome do usuário ou chore que executou o processo

# =================================================================================
# Variáveis do padrão CTI que devem ser alteradas
# =================================================================================


# Informações dos cubos de Origem e Destino
# ========================================
cCuboOrigem		= 'ACO.200.Despesas';
cCuboDestino		= 'ACO.300.Despesas';



# =================================================================================
# Variáveis do padrão CTI não devem ser alteradas
# =================================================================================

# Recupera o usuário que executou o processo
# ========================================
cUsuario = IF( DimIx( '}Clients' , TM1User() ) = 0, 'Executed by Chore' , ATTRS('}Clients', TM1User(), '}TM1_DefaultDisplayValue') );
  

  
# Variáveis de controle do processo
# ========================================
cCuboParametro			= 'SYS.150.Controle_Cargas';
cCuboPropriedade			= '}CubeProperties';
cHorarioInicio			= Now();
cProcesso			= GetProcessName();
cMensagemRegistro	 	= '';
cMensagemProcesso 		= '';
cExisteRegistroRejeitado 		= 0;
cExisteCabecalhoRejeitado 		= 0;
cQtdRegistroRejeitado		= 0;
cTipoErro				= 0;
cContadorRegistro 			= 0;



# Variáveis do cubo de controle
# ========================================
cCaminhoArquivoRejeitado  		= CellGetS( cCuboParametro , cProcesso , 'Caminho do Arquivo de Rejeitado' );
cUtilizarDebug		 	= CellGetS( cCuboParametro , cProcesso , 'Debug (S/N)' );
cGravarLogAlteracao		= CellGetS( cCuboParametro , cProcesso , 'Gerar Log de Alteração (S/N)' );



# Tratamento para os campos vindos do cubo de controle que recebem (S/N)
# ========================================
cUtilizarDebug		 	= IF( cUtilizarDebug @= 'S' , 'S' , 'N' );
cGravarLogAlteracao		= IF( cGravarLogAlteracao @= 'S' , 'S' , 'N' );



# =================================================================================
# Define o Log inicial da carga como falha
# Registra esse log logo no inicio para garantir que tenha algum log de falha registrado
# Caso o processo não consiga chegar no próximo registro de log por alguma falha inesperada
# =================================================================================
cMensagemProcesso	= 'Processo com falha, contate o administrador para verificar os logs de erro do sistema' ;
s01			= TimSt( cHorarioInicio , '\Y-\m-\d \hh\im\ss' );
s02 			= cUsuario ;
s03 			= cMensagemProcesso ;
s04 			= '0' ;
s05 			= TimSt( cHorarioInicio - Now, '\Y-\m-\d \hh\im\ss' );

CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Inicio' );
CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Fim' );
CellPutS( s02 , cCuboParametro, cProcesso, 'Usuário' );
CellPutS( s03 , cCuboParametro, cProcesso, 'Mensagem de Log' );
CellPutS( s04 , cCuboParametro, cProcesso, 'Registros Processados' );
CellPutS( s05 , cCuboParametro, cProcesso, 'Tempo de Execução' );



# =================================================================================
# Validação dos parâmetros
# Teste 1 - O parâmetro só será validado caso exista uma dimensão atrelada
# Teste 2 - Parâmetro não informado
# Teste 3 - Se o parâmetro informado existe na dimensão de validação
# =================================================================================

# Força 2 digitos no parâmetro pMes
# ========================================
pMes = FILL( '0', 2 - LONG( Trim( pMes ) ) ) | Trim( pMes );



# Parâmetro 1
# =========================
sDim = 'ACO.D.ANO';
sElm = pAno;

If( Trim( sDim ) @<> '' );
  If( Trim( sElm ) @= '' );
    cTipoErro = 1;
    DataSourceType = 'NULL';
    cMensagemProcesso = 'O parâmetro    [ ' | sDim | ' ]    não foi Informado' ;
    ItemReject( cMensagemProcesso );
  ElseIf( DimIx( sDim , sElm ) = 0 );
    cTipoErro = 1;
    DataSourceType = 'NULL';
    cMensagemProcesso = 'O elemento    [ ' | sElm | ' ]    não existe na dimensão    [ ' | sDim | ' ]';
    ItemReject( cMensagemProcesso );
  EndIf;
EndIf;



# Parâmetro 2
# =========================
sDim = 'ACO.D.Mes';
sElm = pMes;

If( Trim( sDim ) @<> '' );
  If( Trim( sElm ) @= '' );
    cTipoErro = 1;
    DataSourceType = 'NULL';
    cMensagemProcesso = 'O parâmetro    [ ' | sDim | ' ]    não foi Informado' ;
    ItemReject( cMensagemProcesso );
  ElseIf( DimIx( sDim , sElm ) = 0 );
    cTipoErro = 1;
    DataSourceType = 'NULL';
    cMensagemProcesso = 'O elemento    [ ' | sElm | ' ]    não existe na dimensão    [ ' | sDim | ' ]';
    ItemReject( cMensagemProcesso );
  EndIf;
EndIf;



# Parâmetro 3
# =========================
sDim = 'ACO.D.Versao';
sElm = pVersao;

If( Trim( sDim ) @<> '' );
  If( Trim( sElm ) @= '' );
    cTipoErro = 1;
    DataSourceType = 'NULL';
    cMensagemProcesso = 'O parâmetro    [ ' | sDim | ' ]    não foi Informado' ;
    ItemReject( cMensagemProcesso );
  ElseIf( DimIx( sDim , sElm ) = 0 );
    cTipoErro = 1;
    DataSourceType = 'NULL';
    cMensagemProcesso = 'O elemento    [ ' | sElm | ' ]    não existe na dimensão    [ ' | sDim | ' ]';
    ItemReject( cMensagemProcesso );
  EndIf;
EndIf;



# =================================================================================
# Validação do arquivo de rejeitados na pasta
# Teste 1 - Se o caminho do arquivo rejeitado foi preenchido no cubo de controle
# Teste 2 - Se o diretório do arquivo de rejeitado/debug existir
# =================================================================================


# Define o nome dos arquivos
# ======================================================
cSufixoRejeitado		= IF( cUtilizarDebug @= 'S' , 'Debug' , 'Reject' );
 
cNomeArquivoRejeitado	=  cProcesso 
  | '_' | pAno
  | '_' | pMes
  | '_' | pVersao | '_' | cSufixoRejeitado | '.csv';



# Aborta o processo se o caminho do arquivo reject não foi informado
# ======================================================
IF( cCaminhoArquivoRejeitado  @= '' );
   cTipoErro = 1;
   cMensagemProcesso = 'O caminho onde deverá ser salvo o arquivo com registros rejeitados não foi informado, informe no cubo:   "' | cCuboParametro | '"' ;
   DataSourceType = 'NULL';
   ItemReject( cMensagemProcesso );
ENDIF;



# Insere o caractere "\" no final do caminho caso ainda não tenha
# ======================================================
cCaminhoArquivoRejeitado  	= IF( SUBST( cCaminhoArquivoRejeitado  , LONG( cCaminhoArquivoRejeitado  ) , 1 ) @<> '\' , cCaminhoArquivoRejeitado  | '\' , cCaminhoArquivoRejeitado  );



# Aborta o processo se o diretorio dos arquivos rejeitados não existir
# ======================================================
IF( FileExists( cCaminhoArquivoRejeitado) = 0 );
   cTipoErro = 1;
   cMensagemProcesso = 'O diretório especificado para o arquivo com registros rejeitados  "' | cCaminhoArquivoRejeitado | '"   não existe, informe no cubo:   "' | cCuboParametro | '"' ;
   DataSourceType = 'NULL';
   ItemReject( cMensagemProcesso );
ENDIF;



# Monta o caminho completo 
# ======================================================
cNomeArquivoRejeitadoCompleto	= cCaminhoArquivoRejeitado | cNomeArquivoRejeitado;



# Apaga o arquivo de reject caso exista
# ======================================================
IF( FileExists( cNomeArquivoRejeitadoCompleto ) = 1 );
   ASCIIDelete( cNomeArquivoRejeitadoCompleto );
ENDIF;



# =================================================================================
#
# Definição View de Limpeza do Cudo de Destino
#
# =================================================================================

cSuprimirConsolidado	= 'S';
cSuprimirZero		= 'S'; 
cSuprimirCamposCalculados	= 'S';
cViewTemporaria	 	= 'S';
nViewTemporaria		= If( cViewTemporaria @= 'S', 1, 0);



# Busca parametro para ativação de Log do Cubo
# ======================================================
sLog       	= IF ( cGravarLogAlteracao @= 'N', 'NO', 'YES' );



# Grava no cubo de Log
# ======================================================
CellPutS( sLog, cCuboPropriedade, cCuboDestino, 'LOGGING' );



# Define nome da view e subset de limpeza
# ======================================================
cViewDestino	= 'processo.' | cProcesso | '.' | 'Limpeza' | '.' | TimSt( cHorarioInicio, '\Y-\m-\d \hh\im\ss' );
cSubsetDestino	= cViewDestino;



# Cria a view que será utilizada para a limpeza
# ======================================================
ViewCreate( cCuboDestino , cViewDestino , nViewTemporaria );



# Cria os subsets que serão utilizados para filtrar a view de limpeza
# ======================================================

cDimensao	= 'ACO.D.Ano';
cElemento	= pAno;
If( cDimensao @<> '');
  SubsetCreate( cDimensao, cSubsetDestino , nViewTemporaria );  
  ViewSubsetAssign( cCuboDestino , cViewDestino, cDimensao , cSubsetDestino );  
  SubsetElementInsert( cDimensao, cSubsetDestino, cElemento	, 1 );
EndIf;



cDimensao	= 'ACO.D.Mes';
cElemento	= pMes;
If( cDimensao @<> '');
  SubsetCreate( cDimensao, cSubsetDestino , nViewTemporaria );  
  ViewSubsetAssign( cCuboDestino , cViewDestino, cDimensao , cSubsetDestino );  
  SubsetElementInsert( cDimensao, cSubsetDestino, cElemento	, 1 );
EndIf;



cDimensao	= 'ACO.D.Versao';
cElemento	= pVersao;
If( cDimensao @<> '');
  SubsetCreate( cDimensao, cSubsetDestino , nViewTemporaria );  
  ViewSubsetAssign( cCuboDestino , cViewDestino, cDimensao , cSubsetDestino );  
  SubsetElementInsert( cDimensao, cSubsetDestino, cElemento	, 1 );
EndIf;



# Cria os subsets a partir de MDX
# ======================================================
cDimensao	= '';
cMDX		= '';
If( cDimensao @<> '');
  SubsetCreatebyMDX(cSubsetDestino, cMDX, nViewTemporaria);
  ViewSubsetAssign( cCuboDestino , cViewDestino, cDimensao , cSubsetDestino );  
  SubsetMDXSet(cDimensao, cSubsetDestino, '');
EndIf;



# Configuração da View
# ======================================================
ViewExtractSkipCalcsSet( cCuboDestino, cViewDestino, If( cSuprimirConsolidado @= 'S', 1, 0) );
ViewExtractSkipZeroesSet( cCuboDestino, cViewDestino, If( cSuprimirZero @= 'S', 1, 0)  );
ViewExtractSkipRuleValuesSet( cCuboDestino, cViewDestino, If( cSuprimirCamposCalculados @= 'S', 1, 0) );



# Apaga os valores da View criada
# ======================================================
ViewZeroOut( cCuboDestino , cViewDestino );






# =================================================================================
#
# Definição View de Leitura do Cubo de Origem
#
# =================================================================================

cSuprimirConsolidado	= 'S';
cSuprimirZero		= 'S'; 
cSuprimirCamposCalculados	= 'N';
cViewTemporaria	 	= 'S';
nViewTemporaria		= If( cViewTemporaria @= 'S', 1, 0);




# Define nome da view e subset de origem
# ======================================================
cViewOrigem	= 'processo.' | cProcesso | '.' | 'Leitura' | '.' | TimSt( cHorarioInicio, '\Y-\m-\d \hh\im\ss' );
cSubsetOrigem	= cViewOrigem;



# Cria a view que será utilizada para a leitura
# ======================================================
ViewCreate( cCuboOrigem , cViewOrigem , nViewTemporaria );



# Cria os subsets que serão utilizados para filtrar a view de leitura
# ======================================================

cDimensao	= 'ACO.D.Ano';
cElemento	= pAno;
If( cDimensao @<> '');
  SubsetCreate( cDimensao, cSubsetOrigem, nViewTemporaria );  
  ViewSubsetAssign( cCuboOrigem , cViewOrigem, cDimensao , cSubsetOrigem );  
  SubsetElementInsert( cDimensao, cSubsetOrigem, cElemento	, 1 );
EndIf;



cDimensao	= 'ACO.D.Mes';
cElemento	= pMes;
If( cDimensao @<> '');
  SubsetCreate( cDimensao, cSubsetOrigem, nViewTemporaria );  
  ViewSubsetAssign( cCuboOrigem , cViewOrigem, cDimensao , cSubsetOrigem );  
  SubsetElementInsert( cDimensao, cSubsetOrigem, cElemento	, 1 );
EndIf;



cDimensao	= 'ACO.D.Versao';
cElemento	= pVersao;
If( cDimensao @<> '');
  SubsetCreate( cDimensao, cSubsetOrigem, nViewTemporaria );  
  ViewSubsetAssign( cCuboOrigem , cViewOrigem, cDimensao , cSubsetOrigem );  
  SubsetElementInsert( cDimensao, cSubsetOrigem, cElemento	, 1 );
EndIf;



# Cria os subsets a partir de MDX
# ======================================================
cDimensao	= '';
cMDX		= '';
If( cDimensao @<> '');
  SubsetCreatebyMDX(cSubsetOrigem, cMDX, nViewTemporaria);
  ViewSubsetAssign( cCuboOrigem , cViewOrigem, cDimensao , cSubsetOrigem );  
  SubsetMDXSet(cDimensao, cSubsetOrigem, '');
EndIf;



# Configuração da View de Leitura
# ======================================================
ViewExtractSkipCalcsSet( cCuboOrigem, cViewOrigem, If( cSuprimirConsolidado @= 'S', 1, 0) );
ViewExtractSkipZeroesSet( cCuboOrigem, cViewOrigem, If( cSuprimirZero @= 'S', 1, 0)  );
ViewExtractSkipRuleValuesSet( cCuboOrigem, cViewOrigem, If( cSuprimirCamposCalculados @= 'S', 1, 0) );



# Define Cubo de Origem como Datasource
# ==================================
DataSourceCubeView = cViewOrigem;
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#DATACOMMENT        Descrição:  Grava dados no cubo acumulando valores.
# ===============================================================================================================

# ===================================================================================
# Gera contador para o arquivo de debug ou registros rejeitados o e quantid. total de registros processados
# ===================================================================================
cContadorRegistro = cContadorRegistro + 1;
sContadorRegistro = NumberToString( cContadorRegistro );




# ===================================================================================
# Marca o registro para rejeitar 
# Caso a variavel não existir na dimensão do Mapa
# ===================================================================================
sDim	= 'MAP.D.020.Conta_Contabil';
sElm	= vConta;
sCol	= 'Conta';

If( cExisteRegistroRejeitado = 0 );
   If( DimIx( sDim , sElm ) = 0 );
      cMensagemRegistro = 'A coluna    [ ' | sCol | ' ]    contêm o valor:    [ ' | sElm | ' ]    que não existe na dimensão:    [ ' | sDim | ' ]';
      cExisteRegistroRejeitado = 1;
   EndIf;
EndIf;



# Busca dep-para de 'Agrupamento Conta' no Mapa
# =======================================
cMap		= 'MAP.020.Conta_Contabil';
sGrupoConta  	= CellGetS ( cMap, sElm , 'Cód Nível 2' );



# ===================================================================================
# Marca o registro para rejeitar 
# Caso a variavel estiver em branco, se não existir na dimensão ou se for um totalizador
# ===================================================================================
sDim	= 'ACO.D.Conta_Contabil_Gerencial';
sElm	= sGrupoConta;
sCol	= 'Grupo Conta';

If( cExisteRegistroRejeitado = 0 );
   If( DimIx( sDim , sElm ) = 0 );
      cMensagemRegistro = 'A coluna    [ ' | sCol | ' ]    contêm o valor:    [ ' | sElm | ' ]    que não existe na dimensão:    [ ' | sDim | ' ]';
      cExisteRegistroRejeitado = 1;
   ElseIf( dType( sDim , sElm ) @= 'C' );
      cMensagemRegistro = 'A coluna    [ ' | sCol | ' ]    contêm o valor:    [ ' | sElm | ' ]    que é um totalizador na dimensão:    [ ' | sDim | ' ]    totalizadores não podem receber carga';
      cExisteRegistroRejeitado = 1;
   EndIf;
EndIf;





# ===================================================================================
# Marca o registro para rejeitar 
# Caso a celula do cubo de destino não puder ser atualizada
# ===================================================================================
If( cExisteRegistroRejeitado = 0 );
    nCellIsUpdateable = CellIsUpdateable( cCuboDestino, vAno, vMes, vVersao, sGrupoConta, vCCusto, vMetrica );
    If( nCellIsUpdateable = 0);
      cExisteRegistroRejeitado = 1;
      cMensagemRegistro = 'A célula do cubo não pode receber a carga de dados neste cruzamento, possivelmente esta celula é calculada ou você não tem permissão de escrever nesta célula';
    EndIf;
EndIf;



# ===================================================================================
# DEBUG ou REJEIÇÃO
# Registra em arquivo os registros
# ===================================================================================
If( cExisteRegistroRejeitado = 1  %  cUtilizarDebug @= 'S' );


  # Escreve o cabeçalho do arquivo de registros rejeitados
  # =================================================================================
  If( cExisteCabecalhoRejeitado = 0);
    AsciiOutput( cNomeArquivoRejeitadoCompleto , 'Ano' , 'Mes' , 'Versao' , 'Conta' , 'Grupo Conta' , 'CCusto' , 'Valor' , 'Num. Registro' , 'Mensagem de Erro');
    cExisteCabecalhoRejeitado = 1;
    cTipoErro = 2;
    If( cUtilizarDebug @= 'S'  );
      cMensagemProcesso = ' Processo concluido com registros salvos no arquivo de Debug, verificar o arquivo:    ' | cNomeArquivoRejeitadoCompleto;
    EndIf;
  EndIf;


  # Escreve o registro no arquivo de debug ou rejeitados
  # =================================================================================
  AsciiOutput( cNomeArquivoRejeitadoCompleto , vAno , vMes , vVersao , vConta , sGrupoConta , vCCusto , NumberToString (nValor) , sContadorRegistro , cMensagemRegistro );


  # Limpa as variaveis e pula o registro
  # ============================
  IF( cExisteRegistroRejeitado = 1 );
     cExisteRegistroRejeitado = 0;
     cMensagemRegistro = '';
     cMensagemProcesso = ' Processo concluido com registros rejeitados, verificar o arquivo:    ' | cNomeArquivoRejeitadoCompleto;
     ItemSkip;
  EndIf;

EndIf;



# ===================================================================================
# Grava os dados no cubo
# ===================================================================================
s01 = vAno ;
s02 = vMes ;
s03 = vVersao ;
s04 = sGrupoConta ;
s05 = vCCusto ;
s06 = vMetrica ;

n01 = nValor;

CellIncrementN( n01, cCuboDestino, s01, s02, s03, s04, s05, s06 );


#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****


# ===============================================================================================================
#EPILOGCOMMENT        Descrição:  Habilita log do cubo de destino, realiza CubeSaveData e registra log de status do processo.
# ===============================================================================================================

# =================================================================================
# Habilita o Log e salva dados no cubo
# =================================================================================
CellPutS ( 'YES', cCuboPropriedade, cCuboDestino, 'LOGGING' );
CubeSaveData ( cCuboDestino );


# =================================================================================
# Registra o Log de Erro ou Sucesso no Cubo de Controle
# =================================================================================
If( cTipoErro = 0 );
   cMensagemProcesso  = ' Processo concluído com sucesso';
EndIf;


s01	= TimSt( cHorarioInicio , '\Y-\m-\d \hh\im\ss' );
s02 	= cUsuario ;
s03 	= cMensagemProcesso ;
s04 	= sContadorRegistro  ;
s05 	= TimSt( Now - cHorarioInicio, ' \h:\i:\s') ;

CellPutS( s01 , cCuboParametro, cProcesso, 'DataHora Fim' );
CellPutS( s02 , cCuboParametro, cProcesso, 'Usuário' );
CellPutS( s03 , cCuboParametro, cProcesso, 'Mensagem de Log' );
CellPutS( s04 , cCuboParametro, cProcesso, 'Registros Processados' );
CellPutS( s05 , cCuboParametro, cProcesso, 'Tempo de Execução' );


If( cTipoErro = 2 );
  ItemReject( cMensagemProcesso );
EndIf;
#endregion